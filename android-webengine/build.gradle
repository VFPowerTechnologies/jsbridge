buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$versions.androidGradle"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "com.github.dcendents:android-maven-gradle-plugin:$versions.androidMavenGradle"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion versions.androidCompileSdk
    buildToolsVersion versions.androidBuildTools

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

install {
    repositories.mavenInstaller {
        pom.version = rootProject.version
        pom.groupId = rootProject.group
        pom.artifactId = 'jsbridge-android-webengine'
    }
}

//for other projects this info is configured using the maven-publishing plugin
//since we can't use that here, we need to properly modify the dep info (mostly for the artifactId)
def installer = install.repositories.mavenInstaller

[installer]*.pom*.whenConfigured { pom ->
    def coreDep = pom.dependencies.find { it.artifactId == 'core' }
    coreDep.version = rootProject.version
    coreDep.groupId = rootProject.group
    coreDep.artifactId = 'jsbridge-core'
}

dependencies {
    compile project(':core')
}

//map to the same publish task as the other modules
task publishToMavenLocal(dependsOn: install)
