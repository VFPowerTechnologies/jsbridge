group 'com.vfpowertech'
version '0.4.0'

buildscript {
    ext {
        versions = [
            kotlin: '1.1.1',
            jackson: '2.8.1',
            slf4j: '1.7.15',
            logbackClassic: '1.1.3',
            kovenant: '3.3.0',
            roboVM: '1.13.0',
        ]
    }
}

def projectsToPublish = [
    ':core',
    ':processor',
    ':processor-annotations',
    ':desktop-webengine',
    //':ios-webengine',
].collect { project(it) }

configure(projectsToPublish) {
    apply plugin: 'maven-publish'

    project.afterEvaluate { p ->
        task sourceJar(type: Jar) {
            from sourceSets.main.allSource
        }

        publishing {
            publications {
                maven(MavenPublication) {
                    groupId = rootProject.group
                    version = rootProject.version
                    artifactId = "jsbridge-$p.name"

                    from components.java

                    artifact sourceJar {
                        classifier 'sources'
                    }
                }
            }
        }
    }
}

subprojects {
    repositories {
        jcenter()
    }

    project.afterEvaluate { p ->
        p.dependencies {
            compile "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
        }
    }
}
