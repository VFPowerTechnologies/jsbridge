group 'com.vfpowertech'
version '0.2.0-SNAPSHOT'

buildscript {
    ext {
        versions = [
            kotlin: '1.0.0',
            jackson: '2.6.4',
            slf4j: '1.7.13',
            logbackClassic: '1.1.3',
            kovenant: '3.0.0',
            roboVM: '1.13.0',
            androidGradle: '1.3.1',
            androidMavenGradle: '1.3',
            androidBuildTools: '23.0.2',
            androidCompileSdk: 19,
        ]
    }
}

def projectsToPublish = [
    ':core',
    ':processor',
    ':processor-annotations',
    ':desktop-webengine',
    ':ios-webengine',
    //see android-webengine's build.gradle for android config
    //':android-webengine',
].collect { project(it) }

configure(projectsToPublish) {
    apply plugin: 'maven-publish'

    project.afterEvaluate { p ->
        task sourceJar(type: Jar) {
            from sourceSets.main.allSource
        }

        publishing {
            publications {
                maven(MavenPublication) {
                    groupId = rootProject.group
                    version = rootProject.version
                    artifactId = "jsbridge-$p.name"

                    from components.java

                    artifact sourceJar {
                        classifier 'sources'
                    }
                }
            }
        }
    }
}

subprojects {
    repositories {
        jcenter()
    }

    project.afterEvaluate { p ->
        p.dependencies {
            compile "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
        }
    }
}
